value_change_dump_definitions ::= declaration_command* simulation_command*
declaration_command ::=
    "$comment" comment_text "$end"
  | "$date" date_text "$end"
  | "$enddefinitions" "$end"
  | "$scope" scope_type scope_identifier "$end"
  | "$timescale" time_number time_unit "$end"
  | "$upscope" "$end"
  | "$var" var_type size identifier_code reference "$end"
  | "$version" version_text system_task "$end"
simulation_command ::=
    "$dumpall" value_change+ "$end"
  | "$dumpoff" value_change+ "$end"
  | "$dumpon" value_change+ "$end"
  | "$dumpvars" value_change+ "$end"
  | "$comment" comment_text "$end"
  | simulation_time
  | value_change
scope_type ::=
    "begin"
  | "fork"
  | "function"
  | "module"
  | "task"
time_number ::= "1" | "10" | "100"
time_unit ::= "s" | "ms" | "us" | "ns" | "ps" | "fs"
var_type ::= "event" | "integer" | "parameter" | "real" | "realtime" | "reg" | "supply0" | "supply1" | "time" | "tri" | "triand" | "trior" | "trireg" | "tri0" | "tri1" | "wand" | "wire" | "wor"
simulation_time ::= "#" decimal_number
value_change ::= scalar_value_change | vector_value_change
scalar_value_change ::= value identifier_code
value ::= "0" | "1" | "x" | "X" | "z" | "Z"
vector_value_change ::=
    "b" binary_number identifier_code
  | "B" binary_number identifier_code
  | "r" real_number identifier_code
  | "R" real_number identifier_code
identifier_code ::= .+
size ::= decimal_number
reference ::=
    identifier
  | identifier "[" bit_select_index"]"
  | identifier "[" msb_index ":" lsb_index "]"
index ::= decimal_number
scope_identifier ::= .+
comment_text ::= .+
date_text ::= .+
version_text ::= .+
system_task ::= "$" .+